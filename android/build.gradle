apply plugin: 'com.github.ben-manes.versions'

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
          classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
          classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

project.ext.androidapkprojects = ["android"]
project.ext.androidlibraryprojects = ["spatialite-android"]
project.ext.androidprojects = project.androidlibraryprojects + project.androidapkprojects

def filterProjects(filter) {
    return subprojects.findAll { project -> filter.contains(project.name) }
}
configure(filterProjects(project.androidlibraryprojects)) {
    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'

    android.libraryVariants.all { variant ->
        def name = variant.buildType.name
        if (name.equals("debug")) {
            print "Skipping debug jar"
            return; // Skip debug builds.
        }
        def task = project.tasks.create "jar${name.capitalize()}", Jar
        task.dependsOn variant.javaCompile
        task.from variant.javaCompile.destinationDir
        artifacts.add('archives', task);
    }

    if (project.hasProperty("SONATYPE_USERNAME")) {
        afterEvaluate {
            project.apply from: "${rootProject.projectDir}/deploy.gradle"
        }
    }
}

configure(filterProjects(project.androidprojects)) {

    android {
        compileSdkVersion 25
        buildToolsVersion '25.0.2'
        defaultConfig {
            minSdkVersion 22
            targetSdkVersion 23
        }

        lintOptions {
            abortOnError false
        }
    }
}